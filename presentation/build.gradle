apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
//apply plugin: 'io.fabric'

def keysPropertiesFile = rootProject.file("keys.properties")
def keysProperties = new Properties()
keysProperties.load(new FileInputStream(keysPropertiesFile))
def newsApiKey = keysProperties['NEWS_API_KEY']

android {
    compileSdkVersion Versions.compileSdk
    defaultConfig {
        applicationId ApplicationId.id
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Releases.versionCode
        versionName Releases.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            versionNameSuffix '-debug'
            buildConfigField "String", "NEWS_API_KEY", newsApiKey
        }

        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "NEWS_API_KEY", newsApiKey
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation Libraries.kotlin
    implementation Libraries.appcompat
    implementation Libraries.recyclerView
    implementation Libraries.material
    implementation Libraries.coreKtx
    implementation Libraries.constraintLayout
    implementation Libraries.gson
    implementation Libraries.lifecycle
    kapt Libraries.lifecycleKapt
    // Coroutines
    implementation Libraries.coroutinesCore
    implementation Libraries.coroutinesAndroid
    // DI
    implementation Libraries.koin
    implementation Libraries.koinViewModel
    // Networking
    implementation Libraries.retrofit
    implementation Libraries.retrofitConverter
    implementation Libraries.okHttpLogging
    // Room
    implementation Libraries.room
    implementation Libraries.roomKtx
    kapt Libraries.roomCompiler
    // Navigation
    implementation Libraries.navigation
    implementation Libraries.navigationUI
    // Stetho
    implementation Libraries.stetho
    // Timber
    implementation Libraries.timber

    implementation Libraries.swipeRefreshLayout

    // Firebase
//    implementation Libraries.firebaseAnalytics
//    implementation Libraries.firebaseCrashlytics

    // Testing
    testImplementation TestLibraries.junit
    testImplementation TestLibraries.koinTest
    androidTestImplementation TestLibraries.runner
    androidTestImplementation TestLibraries.espressoCore

    implementation project(':domain')
    implementation project(':data')

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
